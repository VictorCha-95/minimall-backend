plugins {
	id 'java'
	id 'org.springframework.boot' version '3.5.6'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'jacoco'
}

group = 'com.minimall'
version = '0.0.1-SNAPSHOT'
description = 'Demo project for Spring Boot'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation "org.springframework.security:spring-security-crypto"
	implementation 'org.mapstruct:mapstruct:1.5.5.Final'
	implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.13'
	implementation 'io.jsonwebtoken:jjwt-api:0.13.0'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	annotationProcessor 'org.mapstruct:mapstruct-processor:1.5.5.Final'
	annotationProcessor 'org.projectlombok:lombok-mapstruct-binding:0.2.0'
	compileOnly 'org.projectlombok:lombok'
	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	runtimeOnly 'com.mysql:mysql-connector-j'
	runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.13.0'
	runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.13.0'
	annotationProcessor 'org.projectlombok:lombok'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'com.h2database:h2'
	testImplementation 'org.springframework.boot:spring-boot-testcontainers'
	testImplementation 'org.testcontainers:testcontainers'
	testImplementation 'org.testcontainers:mysql'
	testImplementation 'org.testcontainers:junit-jupiter'
	testImplementation 'org.springframework.security:spring-security-test'
}

jacoco {
	toolVersion = "0.8.14"
}

def jacocoExcludes = [
		"**/*Application*",
		"**/config/**",
		"**/dto/**",
		"**/mapper/**",
		"**/generated/**",
		"**/Q*.*",

		"**/exception/**",
		"**/domain/order/discount/**"    // 할인 정책(미사용)
]

tasks.named('test') {
	useJUnitPlatform()
	systemProperty "junit.jupiter.execution.parallel.enabled", "false"
	finalizedBy tasks.named('jacocoTestReport')
}

tasks.named('jacocoTestReport', JacocoReport) {
	dependsOn tasks.named('test')

	classDirectories.setFrom(
			sourceSets.main.output.classesDirs.asFileTree.matching {
				exclude jacocoExcludes
			}
	)

	reports {
		html.required = true
		xml.required = true
		csv.required = false
	}
}